program        → declaration* EOF ;

declaration    → varDecl
               | funDecl
               | classDecl
               | statement ;

varDecl        → "var" IDENTIFIER ( "=" expression )? ";" ;

funDecl        → "fun" function ;
function       → IDENTIFIER "(" parameters? ")" block ;
parameters     → IDENTIFIER ( "," IDENTIFIER )* ;

classDecl      → "class" IDENTIFIER "{" function* "}" ;

statement      → exprStmt
               | ifStmt
               | printStmt
               | whileStmt
               | block
               | returnStmt
               | break ;

exprStmt       → expression ";" ;
ifStmt         → "if" "(" expression ")" statement ( "else" statement )? ;
printStmt      → "print" expression ";" ;
whileStmt      → "while" "(" expression ")" statement ;
forStmt        → "for" "(" ( varDecl | exprStmt | ";" )
                           expression? ";"
                           expression? ")" statement ;
block          → "{" declaration* "}" ;
returnStmt     → "return" expression? ";" ;

expression     → assignment ( "," expression )? ;
assignment     → ( call "." )? IDENTIFIER assignment_op assignment
               | conditional ;
conditional    → logic_or ( "?" expression : conditional )? ;
logic_or       → logic_and ( "or" logic_and )* ;
logic_and      → equality ( "and" equality )* ;
equality       → comparison ( ( "!=" | "==" ) comparison )* ;
comparison     → addition ( ( ">" | ">=" | "<" | "<=" ) addition )* ;
addition       → multiplication ( ( "-" | "+" ) multiplication )* ;
multiplication → unary ( ( "/" | "*" | "%" ) unary )* ;
unary          → ( "!" | "-" | "++" | "--" ) unary
               | postfix ;
postfix        → ( call "." )? IDENTIFIER ( "++" | "--" )
               | call
call           → primary ( "(" arguments? ")" | "." IDENTIFIER )* ;
primary        → "false" | "true" | "nil"
               | NUMBER | STRING
               | IDENTIFIER
               | "this"
               | anonymous_fun
               | "(" expression ")"

anonymous_fun  → "fun" "(" parameters? ")" block ;

assignment_op  → "=" | "+=" | "-=" | "*=" | "/=" | "%=" ;
arguments      → assignment ( "," assignment )* ;
